@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Authenticattion Log'
@Metadata.ignorePropagatedAnnotations: true
@Metadata.allowExtensions: true

define root view entity ZIR_AUTH_LOG
  as select from zuam_auth_log
    composition [0..*] of ZI_ACTIVITY_LOG as _Activity
{
  key session_id    as SessionId,
      event_id      as EventId,
      username      as Username,
      login_date    as LoginDate,
      login_time    as LoginTime,
      login_result  as LoginResult,
      login_message as LoginMessage,
      logout_date   as LogoutDate,
      logout_time   as LogoutTime,
      client        as UserClient,
      terminal_id   as TerminalId,
      system_id     as SystemId,
      mail_sent     as MailSent,
      erzet         as CreateAt,
      erdat         as CreateOn,

      case login_result
      when 'SUCCESS' then 3  // Green
      when 'FAIL'    then 1  // Red
      else 0                 // Neutral
      end           as LoginResultCriticality,

      case event_id
        when 'AU1' then 3
        when 'AU2' then 1
        when 'AUM' then 1
        when 'BU1' then 1
        else 0
      end           as EventCriticality,
      
      _Activity
}
